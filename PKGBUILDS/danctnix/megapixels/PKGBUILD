# Maintainer: Danct12 <danct12@disroot.org>
pkgname=megapixels
pkgver=1.8.3
pkgrel=1
pkgdesc="The Linux-phone camera application"
url="https://gitlab.com/megapixels-org/Megapixels"
license=('GPL-3.0-or-later')
arch=('x86_64' 'armv7h' 'aarch64')
depends=('feedbackd' 'glib2' 'gtk4' 'imagemagick' 'libraw' 'zbar')
makedepends=('meson' 'ninja')
optdepends=('postprocessd: additional postprocess config')
replaces=('pinhole' 'gnome-camera')
source=("https://gitlab.com/megapixels-org/Megapixels/-/archive/${pkgver}/Megapixels-${pkgver}.tar.gz"
        '90-megapixels.rules')
sha256sums=('898d45ab6dce5ff532e8554b90045dc7be2682c32ac7d48573438c7827a34931'
            'c9ff69c5963f6231ac6a7de5adc3e0327f47b501de9b084b49546bfb00e7fc58')

build() {
    arch-meson ${pkgname^}-$pkgver build
    ninja -C build
}

package() {
    DESTDIR="$pkgdir" ninja -C build install

    install -Dm644 "${srcdir}/90-megapixels.rules" -t "${pkgdir}/usr/lib/udev/rules.d/"
}
