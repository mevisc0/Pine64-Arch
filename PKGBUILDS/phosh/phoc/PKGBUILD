# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phoc
pkgver=0.44.0
pkgrel=1
pkgdesc="wlroots based Wayland compositor for mobile phones"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://phosh.mobi"
license=('LGPL-2.1-or-later AND GPL-3.0-or-later AND MIT AND (GPL-3.0-or-later or MIT)')
depends=(
  'gobject-introspection'
  'gnome-desktop'
  'libglvnd'
  'libgmobile'
  'libinput'
  'libliftoff'
  'libxcb'
  'mutter'
  'seatd'
  'xcb-util-errors'
  'xcb-util-renderutil'
  'xcb-util-wm'
  'wayland-protocols'
)
makedepends=(
  'gi-docgen'
  'glib2-devel'
  'glslang'
  'meson'
  'python-docutils'
  'vulkan-headers'
)
checkdepends=('xorg-server-xvfb')
source=(https://sources.phosh.mobi/releases/$pkgname/$pkgname-$pkgver.tar.xz{,.asc})
b2sums=('47f4ad8b5fed5527eee460e1130273b22fe4c9d7aa1a0984750036915f2faea2a6eae606a64eda360250fe89491308f3f54a73d764c324bf553706edb8f664d5'
        'SKIP')
validpgpkeys=('0DB3932762F78E592F6522AFBB5A2C77584122D3')

prepare() {
  cd $pkgname-$pkgver/subprojects/wlroots
  patch -p1 -i "../packagefiles/wlroots/0001-Revert-layer-shell-error-on-0-dimension-without-anch.patch"
}

build() {
  local meson_options=(
    --wrap-mode default
    -D embed-wlroots=enabled
    -D gtk_doc=true
    -D man=true
    --default-library=static
  )

  arch-meson $pkgname-$pkgver build "${meson_options[@]}"
  meson compile -C build
}

check() {
  WLR_RENDERER=pixman xvfb-run meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  # Install scale-to-fit helper
  install -Dm755 "$pkgname-$pkgver"/helpers/scale-to-fit "$pkgdir"/usr/bin/scale-to-fit

  # Remove unnecessary files
  rm -r "$pkgdir"/usr/lib
  rm -r "$pkgdir"/usr/include
}
