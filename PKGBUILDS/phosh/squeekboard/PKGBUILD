# Maintainer: Danct12 <danct12@disroot.org>
pkgname=squeekboard
pkgver=1.43.1
pkgrel=1
pkgdesc="An on-screen-keyboard input method for Wayland"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://gitlab.gnome.org/World/Phosh/squeekboard"
license=('GPL-3.0-or-later')
depends=(
  'feedbackd'
  'glib2'
  'gnome-desktop'
  'gtk3'
  'libbsd'
  'wayland-protocols'
)
makedepends=(
  'glib2-devel'
  'meson'
  'ninja'
  'python-packaging'
  'rust'
)
source=(https://gitlab.gnome.org/World/Phosh/squeekboard/-/archive/v$pkgver/squeekboard-v$pkgver.tar.gz)

build() {
    arch-meson ${pkgname}-v${pkgver} output
    ninja -C output
}

check() {
    meson test -C output --print-errorlogs
}

package() {
    DESTDIR="$pkgdir" ninja -C output install
}

b2sums=('2b8074ece0511214ff2b1a11ab5cfcad14ebe358b003adcd6f696e1d8248103219b2b715671e795b982fd408e341220b6137b696dd779f2f0590cf2b6ab5b204')
