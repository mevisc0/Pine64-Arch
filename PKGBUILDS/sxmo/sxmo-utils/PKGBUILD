# Maintainer: Aren <aren@peacevolution.org>
# Contributor: dni <office@dnilabs.com>

pkgname=('sxmo-utils' 'sxmo-utils-sway' 'sxmo-utils-dwm')
pkgver=1.16.3
pkgrel=1
pkgdesc="Utility scripts, programs, and configs that hold the sxmo UI environment together"
url="https://git.sr.ht/~mil/sxmo-utils"
arch=('x86_64' 'armv7h' 'aarch64')
license=('AGPL3')
makedepends=('libx11' 'xorgproto' 'linux-headers' 'busybox' 'scdoc' 'git')
install=sxmo-utils.install
source=("git+https://git.sr.ht/~mil/sxmo-utils#tag=$pkgver"
        '0001-avoid-conflicting-with-systemd-services.patch'
        '0002-Partially-revert-7a77851c.patch'
        '00-sxmo.rules'
        '10-sxmo.conf')
sha512sums=('db923a70658862106b34ed2c81c1335e41c8857b3216fd7e64ea6e594a1feef670451968946dc45bea8dd6c7e8bfbe3ecd944f85c0bddddf3f1f5700b1664f04'
            '2a11f00fa5f508a0a5af3fb28730317de1cc99bb340ef724e5566aaa9bccec44563da3094028e8f447db63500bbedf46c25c1db250abf771f2e3549038e59224'
            'd327fcc2abdc25889f650d7c47a6b9cc67e85539954b45d4097ff9bffb75133244ea8b0d96339f9c3280a544395dcc9b96d59c6a9a117b7f685afe5123d377eb'
            'd705fc9611716a73c89da94bf3abc9292e9069bc994b0bd097159f4babd83d4693d5cc3e42336739ac46a8c6b4a0f39d077047202901dc5066df75e597450fa7'
            'b69a09f98f8a5cad8164cd48c5f6a78f2ce0e9818fb77ccf2a3e243cd0f2023e438022b20aad9e6b5edd90e1bb862326db79dc423630682d4c43e897090d3da1')


prepare() {
  cd "sxmo-utils"

  patch -p1 < '../0001-avoid-conflicting-with-systemd-services.patch'
  patch -p1 < '../0002-Partially-revert-7a77851c.patch'
}

build() {
  make -C "sxmo-utils"
}

package_sxmo-utils() {
  backup=('etc/doas.conf')
  depends=(
    # Shell utilities
    'bc'
    'curl'
    'gawk'
    'jq'
    'less'
    'net-tools' # netstat command
    'pnc'

    # Core dependcies
    'callaudiod'
    'opendoas'
    'geoclue'
    'iio-utils'
    'inotify-tools'
    'mmsd-tng'
    'mnc' # for suspend
    'modemmanager'
    'ncurses' # for the weather script
    'networkmanager'
    'superd'
    'vvmd'
    'xdg-user-dirs'

    # Audio
    # TODO: alsa-utils is only required for sxmo_record.sh, it can probably be
    # removed soon
    'alsa-utils'
    'bluez'
    'bluez-utils'
    'libpulse'
    'pulse-native-provider'

    # Core GUI dependencies
    'brightnessctl'
    'conky'
    'dunst'
    'libnotify'
    'lisgd'
    'mpv'
    'sxmo-ui'
    'ttf-dejavu-nerd'
    'upower')
  optdepends=('bemenu-ncurses: sxmo menus over ssh'
              'bonsai: better multikey script'
              'clickclack: haptic feedback'
              'codemadness-frontends: Youtube & Reddit scripts'
              'j4-dmenu-desktop: all apps menu generated from .desktop files'
              'mediainfo: sound recorder'
              'pipewire-pulse: recommended sound server'
              'sfeed: Rss and atom feeds'
              'vim: The default editor'
              'yt-dlp: Play videos from the web')

  make -C "sxmo-utils" \
    OPENRC=0 \
    DESTDIR="$pkgdir" \
    EXTERNAL_SERVICES=0 \
    install-scripts install-docs

  # Allow access to ModemManager and NetworkManager
  install -Dm644 -t "$pkgdir/usr/share/polkit-1/rules.d" "00-sxmo.rules"

  # Prevent logind from handling the power button
  install -Dm644 -t "$pkgdir/etc/systemd/logind.conf.d" "10-sxmo.conf"

  # Fixup mode to match polkit
  install -d -o root -g 102 -m 750 "$pkgdir/usr/share/polkit-1/rules.d"

  # fix directory permissions, TODO this should be done upstream
  chmod 750 "$pkgdir/etc/doas.d"

  # HACK: doas is built without --with-doas-confdir so install the sxmo config directly
  install -Dm640 "$pkgdir/etc/doas.d/sxmo.conf" "$pkgdir/etc/doas.conf"
}

package_sxmo-utils-sway() {
  pkgdesc="Use sway as the window manager with sxmo"
  url="https://sr.ht/~mil/sxmo/"
  arch=('any')
  provides=('sxmo-ui')
  replaces=('sxmo-ui-sway-meta')
  install=
  depends=('bemenu-wayland'
           'foot'
           'grim'
           'imv'
           'mako'
           'slurp'
           'sway'
           'swaybg'
           'swayidle'
           'wl-clipboard'
           'wob'
           'wtype'
           'wvkbd'
           'xorg-xwayland')

  make -C "sxmo-utils" DESTDIR="$pkgdir" install-sway
}

package_sxmo-utils-dwm() {
  pkgdesc="Packages required to use dwm as a ui for sxmo"
  url="https://sr.ht/~mil/sxmo/"
  arch=('any')
  provides=('sxmo-ui')
  replaces=('sxmo-ui-dwm-meta')
  install=
  depends=('autocutsel'
           'feh'
           'scrot'
           'svkbd'
           'sxiv'
           'sxmo-dmenu'
           'sxmo-dwm'
           'sxmo-st'
           'unclutter'
           'xcalib'
           'xclip'
           'xdotool'
           'xob'
           'xorg-xdpyinfo'
           'xorg-xinput'
           'xorg-xprop'
           'xorg-xrandr'
           'xorg-xrdb'
           'xorg-xset'
           'xorg-xsetroot'
           'xorg-xwininfo'
           'xprintidle'
           'xsel')

  make -C "sxmo-utils" DESTDIR="$pkgdir" install-dwm
}
